buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

enum VersionStatus {
    alpha, beta, release

    @Override
    String toString() {
        switch (this) {
            case alpha: return "α"
            case beta: return "β"
            case release: return "R"
        }
    }
}

compileJava.options.encoding("UTF-8")
compileTestJava.options.encoding("UTF-8")

mixin {
    add sourceSets.main, 'parcool.refmap.json'
    config 'parcool.mixins.json'
}
def config = [ //https://mcforge.readthedocs.io/en/latest/conventions/versioning/
               mc_version   : '1.18.1',//supported minimum Minecraft version
               major_mod    : '2',//Removing items, blocks, tile entities, etc.
               major_api    : '0',//Changing the order or variables of enums. etc
               minor        : '0',//Adding items, blocks, tile entities, etc.
               patch        : '1',//bugfix
               forge_version: '1.18.1-39.1.0',
               versionStatus: VersionStatus.release
]

def shouldLogDetailDebugInfo = true

//Version Format (McVersion-MajorMOD.MajorAPI.Minor.Patch-VersionStatus)
version = "${config.mc_version}-${config.major_mod}.${config.major_api}.${config.minor}.${config.patch}-${config.versionStatus.toString()}"
group = 'com.alrex'
archivesBaseName = 'ParCool'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.18.1'


    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            if (shouldLogDetailDebugInfo) {
                // Recommended logging data for a userdev environment
                property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            }
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            if (shouldLogDetailDebugInfo) {
                // Recommended logging data for a userdev environment
                property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            }
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            if (shouldLogDetailDebugInfo) {
                // Recommended logging data for a userdev environment
                property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            }
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'parcool', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    // Specify the version of Minecraft to use.
    minecraft "net.minecraftforge:forge:${config.forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "ParCool!",
                "Specification-Vendor"    : "alRex",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "alRex",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"            : "parcool.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}